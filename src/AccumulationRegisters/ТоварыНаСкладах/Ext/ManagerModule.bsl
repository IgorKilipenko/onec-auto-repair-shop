#Область ПрограммныйИнтерфейс

// Возвращает остатки по списку **Номенклатуры** из регистра **ТоварыНаСкладах**
// Параметры:
//  списокНоменклатуры - ФиксированныйМассив, Массив из СправочникСсылка.Номенклатура
//  период - Дата, МоментВремени, Граница, Неопределено
//  параметры - ФиксированнаяСтруктура, Структура
//              * Выгрузить - Булево - По умолчанию = Истина
//              * ТолькоОтрицательныеОстатки - Булево - По умолчанию = Ложь
//            - Неопределено
// Возвращаемое значение:
//  - ТаблицаЗначений - Если **выгрузить** = Истина
//  - ВыборкаИзРезультатаЗапроса - Если **выгрузить** = Ложь
//      * Номенклатура - СправочникСсылка.Номенклатура
//      * Склад - СправочникСсылка.Склады
//      * СрокГодности - Дата
//      * Количество - Число
//      * Сумма - Число
//  - Неопределено - Если данные отсутствуют в регистре
Функция ПолучитьОстаткиНоменклатуры(Знач списокНоменклатуры, Знач период = Неопределено, Знач параметры = Неопределено) Экспорт
    ДиагностикаКлиентСервер.Утверждение(ПроверкаТиповКлиентСервер.ЭтоМассив(списокНоменклатуры, Ложь),
        "Список номенклатуры должен иметь Тип ""[Фиксированный]Массив"".");
    ДиагностикаКлиентСервер.Утверждение(параметры = Неопределено ИЛИ ПроверкаТиповКлиентСервер.ЭтоСтруктура(параметры, Ложь),
            "Аргумент ""Параметры"" должен иметь тип ""[Фиксированная]Структура"".");

    параметры = ?(параметры = Неопределено,
            Новый Структура("Выгрузить, ТолькоОтрицательныеОстатки", Истина, Ложь),
            параметры);

    выгрузить = РаботаСКоллекциямиКлиентСервер.ПолучитьЗначениеСтруктуры(
            параметры, "Выгрузить", Истина);
    толькоОтрицательныеОстатки = РаботаСКоллекциямиКлиентСервер.ПолучитьЗначениеСтруктуры(
            параметры, "ТолькоОтрицательныеОстатки", Ложь);

    запрос = Новый Запрос;
    запрос.УстановитьПараметр("Номенклатура", списокНоменклатуры);
    запрос.Текст =
        "ВЫБРАТЬ
        |	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
        |	ТоварыНаСкладахОстатки.Склад КАК Склад,
        |	ТоварыНаСкладахОстатки.СрокГодности КАК СрокГодности,
        |	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество,
        |	ТоварыНаСкладахОстатки.СуммаОстаток КАК Сумма
        |ИЗ
        |	РегистрНакопления.ТоварыНаСкладах.Остатки(&Период, Номенклатура В (&Номенклатура)) КАК ТоварыНаСкладахОстатки
        |ГДЕ &Условие
        |";

    Если период = Неопределено Тогда
        запрос.Текст = СтрЗаменить(запрос.Текст, "&Период, ", "");
    Иначе
        запрос.УстановитьПараметр("Период", период);
    КонецЕсли;

    Если толькоОтрицательныеОстатки Тогда
        запрос.Текст = СтрЗаменить(запрос.Текст, "&Условие", "ТоварыНаСкладахОстатки.КоличествоОстаток < 0");
    Иначе
        запрос.Текст = СтрЗаменить(запрос.Текст, "ГДЕ &Условие", "");
    КонецЕсли;

    результатЗапроса = запрос.Выполнить();
    Если результатЗапроса.Пустой() Тогда
        Возврат Неопределено;
    КонецЕсли;

    Если выгрузить = Истина Тогда
        Возврат результатЗапроса.Выгрузить();
    Иначе
        Возврат результатЗапроса.Выбрать();
    КонецЕсли;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
