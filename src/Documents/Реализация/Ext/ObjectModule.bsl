#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(Знач данныеЗаполнения, __, ___)
    Если ЗначениеЗаполнено(ЭтотОбъект.АвторДокумента) = Ложь Тогда
        ЭтотОбъект.АвторДокумента = ПараметрыСеанса.ТекущийПользователь;
    КонецЕсли;

    Если ТипЗнч(данныеЗаполнения) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
        заполнитьНаОснованииДокументаЗакаНаряд(данныеЗаполнения);
    КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(отказ, __)
    очиститьДвиженияДокумента();

    результатВыполненияДвижений = выполнитьВсеДвиженияДокумента();
    Если результатВыполненияДвижений.Успех = Ложь Тогда
        отказ = Истина;
    Иначе
        записатьДвиженияДокумента();
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции
#Область Движения
Процедура записатьДвиженияДокумента()
    ЭтотОбъект.Движения.ТоварыНаСкладах.Записывать = Истина;
    ЭтотОбъект.Движения.Продажи.Записывать = Истина;
    ЭтотОбъект.Движения.УчетЗатрат.Записывать = Истина;
    ЭтотОбъект.Движения.МатериалыЗаказчиков.Записывать = Истина;
    ЭтотОбъект.Движения.Хозрасчетный.Записывать = Истина;
    ЭтотОбъект.Движения.ЗаказыКлиентов.Записывать = Истина;

    ЭтотОбъект.Движения.Записать();
КонецПроцедуры

Процедура очиститьДвиженияДокумента()
    записатьДвиженияДокумента();
КонецПроцедуры

// Выполняет движения документа по регистрам:
//  * `ТоварыНаСкладах` - Списание товаров (партий товаров) с контролем остатков на складе
//  * `Продажи`
//  * `УчетЗатрат` - Отражение затрат по статьям
//  * `МатериалыЗаказчиков` - Списание материалов заказчика
//  * `Хозрасчетный` - Бухгалтерский учет себестоимости и выручки от продаж
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
Функция выполнитьВсеДвиженияДокумента()
    результатДвижений = Новый Структура("Успех", Истина);

    // Списание товаров со склада (с контролем остатков)
    // Отражение себестоимости продаж (по регистру УчетЗатрат и в бухучете)
    // Отражение выручки от реализации товаров документа (по регистру Продажи)
    результатДвижений.Успех = выполнитьВсеДвиженияТоваровДокумента().Успех;
    Если результатДвижений.Успех = Ложь Тогда
        Возврат результатДвижений;
    КонецЕсли;

    // Отражение выручки от реализации услуг документа (по регистру Продажи)
    выполнитьВсеДвиженияУслугДокумента();

    // Списание материалов заказчика
    выполнитьВсеДвиженияМатериаловЗаказчикаДокумента();

    // Учет выполненных услуг (расход в регистре ЗаказыКлиентов) без контроля остатков
    Если ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд")
        И ЭтотОбъект.ДокументОснование.Пустая() = Ложь Тогда
        выполнитьДвижениеЗаказыКлиентовРасход();
    КонецЕсли;

    // Отражение выручки от продаж в бухучете
    структураВыручки = Новый ФиксированнаяСтруктура("Сумма, Клиент", ЭтотОбъект.СуммаДокумента, ЭтотОбъект.Контрагент);
    ЭтотОбъект.Движения.Хозрасчетный.ДобавитьЗаписьОтраженияВыручкиОтРеализацииТМЦ(ЭтотОбъект.Дата, структураВыручки);
    
    Возврат результатДвижений;
КонецФункции

// Выполняет движения документа для `ТЧ Товары` с контролем остатков товаров на складе.
//  В случае отсутствия достаточного количества товара для списания выдает сообщение пользователю с информацией.
//  Регистры выполняемых движений:
//      * `ТоварыНаСкладах` - Списание товаров (партий товаров) с контролем остатков на складе
//      * `Продажи` - Отражение выручки по номенклатуре товаров
//      * `УчетЗатрат` - Отражение затрат по статьям затрат
//      * `Хозрасчетный` - Бухгалтерский учет себестоимости и выручки от продаж товаров (по партиям)
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
Функция выполнитьВсеДвиженияТоваровДокумента()
    результатДвижения = Новый Структура("Успех", Истина);
    Если ЭтотОбъект.Товары.Количество() = 0 Тогда
        Возврат результатДвижения;
    КонецЕсли;

    // Установка блокировки для контроля остатков на складе
    блокировка = получитьБлокировкуИзмененияТоварыНаСкладах();
    блокировка.Заблокировать();

    // Подготовка данных для выполнения движений
    менеджерТаблиц = Новый МенеджерВременныхТаблиц;
    деревоТоваров = получитьТоварыДокументаИОстатки(менеджерТаблиц);
    ДиагностикаКлиентСервер.Утверждение(деревоТоваров <> Неопределено);

    // Коллекция для аккумуляции сумм себестоимостей списанных товаров по статьям затрат
    структураСтатейЗатрат = Новый Соответствие;

    // Обход номенклатуры товаров документа
    Для Каждого строкаТоваров Из деревоТоваров.Строки Цикл
        // Контроль достаточности остатков на складе для списания
        количествоОстатков = строкаТоваров.КоличествоОстаток - строкаТоваров.КоличествоВДокументе;
        Если количествоОстатков < 0 Тогда // Недостаточно товара на складе
            сообщитьПользователюОПревышенииОстатков(
                -количествоОстатков,
                строкаТоваров.НоменклатураПредставление);

            результатДвижения.Успех = Ложь; // Отказ проведения документа
        КонецЕсли;

        Если результатДвижения.Успех = Ложь Тогда
            // Превышены остатки на складах для товара, далее в итерациях
            //  собираются только сведения о превышениях остатков без выполнения движений
            Продолжить;
        КонецЕсли;

        // Списание товара (партий товара) со склада и отражение себестоимости в бухучете
        себестоимостьСписанныхТоваров = выполнитьСписаниеТовара(строкаТоваров);

        // Отражение выручки от продаж ТМЦ
        выполнитьДвижениеПродажиОборот(строкаТоваров.Номенклатура,
            строкаТоваров.СуммаВДокументе, строкаТоваров.количествоВДокументе);

        // Накопление сумм себестоимостей списанных тавров (по статьям затрат) для учета затрат
        ДиагностикаКлиентСервер.Утверждение(строкаТоваров.СтатьяЗатрат <> Неопределено,
            "Статья затрат должна иметь заполненное значение.");

        текущаяСуммаПоСтатьеЗатрат = структураСтатейЗатрат.Получить(строкаТоваров.СтатьяЗатрат);
        Если текущаяСуммаПоСтатьеЗатрат = Неопределено Тогда
            структураСтатейЗатрат.Вставить(строкаТоваров.СтатьяЗатрат, себестоимостьСписанныхТоваров);
        Иначе
            структураСтатейЗатрат[строкаТоваров.СтатьяЗатрат] = текущаяСуммаПоСтатьеЗатрат + себестоимостьСписанныхТоваров;
        КонецЕсли;
    КонецЦикла;

    // Учет себестоимости реализуемых товаров (по статям затрат)
    Если результатДвижения.Успех И структураСтатейЗатрат.Количество() > 0 Тогда
        Для Каждого элементЗатратКЗ Из структураСтатейЗатрат Цикл
            выполнитьДвижениеУчетЗатратОборот(элементЗатратКЗ.Ключ, элементЗатратКЗ.значение);
        КонецЦикла;
    КонецЕсли;

    Возврат результатДвижения;
КонецФункции

// Выполняет движения документа для `ТЧ Услуги` по регистрам:
//  * Продажи - Отражение выручки от реализации услуг документа
Процедура выполнитьВсеДвиженияУслугДокумента()
    Если ЭтотОбъект.Услуги.Количество() = 0 Тогда
        Возврат;
    КонецЕсли;

    // Подготовка данных для выполнения движений
    услугиДокумента = получитьУслугиДокумента();
    ДиагностикаКлиентСервер.Утверждение(услугиДокумента <> Неопределено);

    Для Каждого строкаУслуг Из услугиДокумента Цикл
        выполнитьДвижениеПродажиОборот(строкаУслуг.Номенклатура, строкаУслуг.Сумма, строкаУслуг.Количество);
    КонецЦикла;
КонецПроцедуры

Процедура выполнитьВсеДвиженияМатериаловЗаказчикаДокумента()
    Если ЭтотОбъект.МатериалыЗаказчика.Количество() = 0 Тогда
        Возврат;
    КонецЕсли;

    Для Каждого строкаМатериалов Из ЭтотОбъект.МатериалыЗаказчика Цикл
        выполнитьДвижениеМатериалыЗаказчиковРасход(строкаМатериалов.Номенклатура, строкаМатериалов.Количество);
    КонецЦикла;
КонецПроцедуры

Процедура выполнитьДвижениеМатериалыЗаказчиковРасход(Знач номенклатураСсылка, Знач количество)
    движение = Движения.МатериалыЗаказчиков.ДобавитьРасход();
    движение.Период = ЭтотОбъект.Дата;
    движение.Склад = ЭтотОбъект.Склад;
    движение.Заказчик = ЭтотОбъект.Контрагент;
    движение.Номенклатура = номенклатураСсылка;
    движение.Количество = количество;
КонецПроцедуры

Процедура выполнитьДвижениеПродажиОборот(Знач номенклатура, Знач сумма, Знач количество)
    движение = Движения.Продажи.Добавить();
    движение.Период = ЭтотОбъект.Дата;
    движение.Номенклатура = номенклатура;
    движение.Сотрудник = ЭтотОбъект.Сотрудник;
    движение.Контрагент = ЭтотОбъект.Контрагент;
    движение.Количество = количество;
    движение.Сумма = сумма;
КонецПроцедуры

// Выполняет списание товара (партий товаров) со склада и отражение себестоимости продаж в бухучете
// Параметры:
//  строкаДереваТоваров - СтрокаДереваЗначений - Строка группировки товаров и материалов документа
//                                                  с детальными данными по партиям (срокам годности)
//      * Номенклатура - СправочникСсылка.Номенклатура
//      * НоменклатураПредставление - Строка
//      * СчетБухгалтерскогоУчета - ПланСчетовСсылка.Хозрасчетный - Счет кредита
//      * КоличествоВДокументе - Число - Общее количество номенклатуры в документе реализации
//      * СуммаВДокументе - Число - Общая стоимость номенклатуры в документе реализации
//      * КоличествоОстаток - Число - Общее количество остатков номенклатуры на складе
//      * Строки - КоллекцияСтрокДереваЗначений
//          ** Номенклатура - СправочникСсылка.Номенклатура
//          ** СрокГодности - Дата - Пария номенклатуры
//          ** СуммаОстаток - Число - Себестоимость остатков партии номенклатуры на складе
// Возвращаемое значение:
//  - Число - Общая себестоимость списанного товара (партий товара)
Функция выполнитьСписаниеТовара(Знач строкаДереваТоваров)
    // Накопление общей стоимости списания всех партий номенклатуры
    общаяСтоимостьНоменклатуры = 0;
    // Накопление количества несписанного остатка номенклатуры документа
    несписанныйОстаток = строкаДереваТоваров.КоличествоВДокументе;

    // Обход партий товара
    Для Каждого партияНоменклатурыНаСкладе Из строкаДереваТоваров.Строки Цикл
        // Списание партии товара со склада
        результатСписания = выполнитьДвижениеТоварыНаСкладахРасход(
                партияНоменклатурыНаСкладе,
                несписанныйОстаток);

        общаяСтоимостьНоменклатуры = общаяСтоимостьНоменклатуры + результатСписания.Сумма;
        несписанныйОстаток = несписанныйОстаток - результатСписания.Количество;

        // Списание себестоимости партии товара по бухгалтерии
        структураСписания = Новый Структура("СчетКредита, Номенклатура, Сумма",
                партияНоменклатурыНаСкладе.СчетБухгалтерскогоУчета, партияНоменклатурыНаСкладе.Номенклатура, результатСписания.Сумма);
        ЭтотОбъект.Движения.Хозрасчетный.ДобавитьЗаписьСписанияСебестоимостиТМЦ(ЭтотОбъект.Дата, структураСписания);

        // Завершение цикла если все партии товара документа списаны со склада
        Если несписанныйОстаток <= 0 Тогда
            ДиагностикаКлиентСервер.Утверждение(несписанныйОстаток = 0,
                    СтрШаблон("Несписанный остаток партии товара: ""%1"" не должен иметь отрицательное значение.",
                        партияНоменклатурыНаСкладе.НоменклатураПредставление));
            Прервать;
        КонецЕсли;
    КонецЦикла;

    Возврат общаяСтоимостьНоменклатуры;
КонецФункции

// Устарела. Необходимо добавить обработку случая когда учет затрат выполняется по средней цене (без учета срока годности)
//
// Параметры:
//  партияНоменклатурыНаСкладе - Структура, ФиксированнаяСтруктура, СтрокаДереваЗначений - Партия номенклатуры (остаток на складе)
//      * Номенклатура - СправочникСсылка.Номенклатура
//      * СрокГодности - Дата - Пария номенклатуры
//      * СуммаОстаток - Число - Себестоимость остатков партии номенклатуры на складе
//  остатокВДокументе - Число - Текущий (не списанный) остаток количества Номенклатуры (товаров и материалов) в документе
// Возвращаемое значение:
//  - Структура
//      * Сумма - Число - Сумма выполненного списания партии номенклатуры
//      * Количество - Число - Количество списанных остатков партии номенклатуры со склада
Функция выполнитьДвижениеТоварыНаСкладахРасход(Знач партияНоменклатурыНаСкладе, Знач остатокВДокументе)
    // Определяем количество остатков партии номенклатуры на складе
    доступноДляСписания = Мин(партияНоменклатурыНаСкладе.КоличествоОстаток, остатокВДокументе);

    движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
    движение.Период = Дата;
    движение.Номенклатура = партияНоменклатурыНаСкладе.Номенклатура;
    движение.Склад = ЭтотОбъект.Склад;
    движение.СрокГодности = партияНоменклатурыНаСкладе.СрокГодности;
    движение.Количество = доступноДляСписания;

    Если партияНоменклатурыНаСкладе.КоличествоОстаток = остатокВДокументе Тогда
        // Списываем всю стоимость остатков партии номенклатуры на складе
        движение.Сумма = партияНоменклатурыНаСкладе.СуммаОстаток;
    Иначе
        // Списываем часть стоимости остатков партии номенклатуры на складе (т.к. количество в документе меньше остатка на складе)
        движение.Сумма = доступноДляСписания / партияНоменклатурыНаСкладе.КоличествоОстаток
            * партияНоменклатурыНаСкладе.СуммаОстаток;
    КонецЕсли;

    результатСписания = Новый Структура("Сумма, Количество", движение.Сумма, движение.Количество);
    Возврат результатСписания;
КонецФункции

Процедура выполнитьДвижениеУчетЗатратОборот(Знач статьяЗатрат, Знач сумма)
    движение = Движения.УчетЗатрат.Добавить();
    движение.Период = ЭтотОбъект.Дата;
    движение.СтатьяЗатрат = статьяЗатрат;
    движение.Сумма = сумма;
КонецПроцедуры

Процедура выполнитьДвижениеЗаказыКлиентовРасход()
    ожидаемыйТипДокумента = Тип("ДокументСсылка.ЗаказНаряд");
    ДиагностикаКлиентСервер.Утверждение(
        ЗначениеЗаполнено(ЭтотОбъект.ДокументОснование)
        И ТипЗнч(ЭтотОбъект.ДокументОснование) = ожидаемыйТипДокумента,
            СтрШаблон("ДокументОснование должен быть заполнен и иметь тип значения ""%1"".",
                Строка(ожидаемыйТипДокумента)));

    движение = Движения.ЗаказыКлиентов.Добавить();
    движение.ВидДвижения = ВидДвиженияНакопления.Расход;

    движение.Период = ЭтотОбъект.Дата;
    движение.Клиент = ЭтотОбъект.Контрагент;
    движение.ЗаказКлиента = ЭтотОбъект.ДокументОснование;
    движение.Сумма = ЭтотОбъект.Услуги.Итог("Сумма");
КонецПроцедуры
#КонецОбласти // Движения

#Область ЗаполнениеНаОсновании
Процедура заполнитьНаОснованииДокументаЗакаНаряд(Знач документСсылка)
    ДиагностикаКлиентСервер.Утверждение(ТипЗнч(документСсылка) = Тип("ДокументСсылка.ЗаказНаряд"));
    ДиагностикаКлиентСервер.Утверждение(ЗначениеЗаполнено(документСсылка),
        "Для заполнения на основании документ должен иметь не пустую ссылку.");

    ЭтотОбъект.ДокументОснование = документСсылка;
    ЭтотОбъект.Контрагент = документСсылка.Клиент;
    ЭтотОбъект.Автомобиль = документСсылка.Автомобиль;
    ЭтотОбъект.Сотрудник = документСсылка.Сотрудник;
    ЭтотОбъект.Комментарий = документСсылка.Комментарий;
    ЭтотОбъект.СуммаДокумента = документСсылка.СуммаДокумента;

    // Заполнение ТЧ Услуги
    ДиагностикаКлиентСервер.Утверждение(ЭтотОбъект.Услуги.Количество() = 0,
            "Таблица Услуги должна быть пустой");
    копироватьТЧ(ЭтотОбъект.Услуги, документСсылка.Услуги);

    // Заполнение ТЧ МатериалыЗаказчика
    ДиагностикаКлиентСервер.Утверждение(ЭтотОбъект.МатериалыЗаказчика.Количество() = 0,
            "Таблица МатериалыЗаказчика должна быть пустой");
    копироватьТЧ(ЭтотОбъект.МатериалыЗаказчика, документСсылка.МатериалыЗаказчика);

КонецПроцедуры

Процедура копироватьТЧ(Знач таблицаПриемник, Знач таблицаИсточник)
    Для Каждого строкаУслугИсточник Из таблицаИсточник Цикл
        новаяСтрокаУслуг = таблицаПриемник.Добавить();
        ЗаполнитьЗначенияСвойств(новаяСтрокаУслуг, строкаУслугИсточник);
    КонецЦикла;
КонецПроцедуры
#КонецОбласти // ЗаполнениеНаОсновании

#Область КонтрольОстатков
// Выполняет проверку достаточности остатков по Номенклатуре.
// В случае отсутствия достаточного количества остатков - оповещает Пользователя.
// Параметры:
//	количествоВДокументе - Число - Количество Номенклатуры в документе для движения
//	остаток - Число - Остаток Номенклатуры на складах
//	наименованиеНоменклатуры - Строка - Наименование Номенклатуры, используется в тексте сообщения
// Возвращаемое значение:
//	Булево - Истина если остатков нехватает, иначе Ложь
Функция проверитьПревышениеОстатков(Знач количествоВДокументе, Знач остаток, Знач наименованиеНоменклатуры)
    превышениеОстатковНоменклатуры = количествоВДокументе - остаток;
    Если превышениеОстатковНоменклатуры > 0 Тогда
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст = СтрШаблон("Превышение остатка по номенклатуре: ""%1"" в количестве: ""%2""",
                наименованиеНоменклатуры, превышениеОстатковНоменклатуры);
        сообщение.Сообщить();
        Возврат Истина;
    КонецЕсли;
    Возврат Ложь;
КонецФункции

// Параметры:
//	превышениеОстатков - Число - Количество превышения остатков
//	наименованиеНоменклатуры - Строка, Неопределено - Наименование Номенклатуры, используется в тексте сообщения
//	записьКлиентаПредставление - Строка, Неопределено - Строковое представление записи клиента
Процедура сообщитьПользователюОПревышенииОстатков(
        Знач превышениеОстатков, Знач наименованиеНоменклатуры = Неопределено, Знач записьКлиентаПредставление = Неопределено)
    ДиагностикаКлиентСервер.Утверждение(наименованиеНоменклатуры <> Неопределено
        ИЛИ записьКлиентаПредставление <> Неопределено,
        "Один из необязательных аргументов: [НаименованиеНоменклатуры, ЗаписьКлиентаПредставление] должен быть заполнен.");

    форматФинансовыхДанных = "ЧДЦ=2; ЧРГ= ; ЧН=0.00";
    сообщение = Новый СообщениеПользователю;
    текстСообщения = "";
    Если записьКлиентаПредставление <> Неопределено Тогда // Это превышение суммы по записи клиента
        текстСообщения = СтрШаблон("Превышена сумма по записи клиента ""%1"" на ""%2"".",
                записьКлиентаПредставление, Формат(превышениеОстатков, форматФинансовыхДанных));
    Иначе
        текстСообщения = СтрШаблон("Превышение остатка по номенклатуре: ""%1"" в количестве: ""%2""",
                наименованиеНоменклатуры, превышениеОстатков);
    КонецЕсли;

    сообщение.Текст = текстСообщения;
    сообщение.Сообщить();
КонецПроцедуры
#КонецОбласти // КонтрольОстатков

#Область ЗапросыДанных
// Получает выборку Номенклатуры текущего документа и остатки на складе по товарам
// Параметры:
//  менеджерТаблиц - МенеджерВременныхТаблиц
// Возвращаемое значение:
//  - ДеревоЗначений
//  - Неопределено
Функция получитьТоварыДокументаИОстатки(Знач менеджерТаблиц)
    ДиагностикаКлиентСервер.Утверждение(ТипЗнч(менеджерТаблиц) = Тип("МенеджерВременныхТаблиц"));

    менеджерТаблиц = Документы.Реализация.ПолучитьТоварыДокумента(ЭтотОбъект.Ссылка, , менеджерТаблиц);
    периодДокумента = Новый Граница(ЭтотОбъект.МоментВремени(), ВидГраницы.Включая);
    Возврат Документы.Реализация.ПолучитьОстаткиИСтатьиЗатратДляТаблицыТоваров(менеджерТаблиц, периодДокумента, ЭтотОбъект.Склад, Истина);
КонецФункции

// Получает выборку Номенклатуры текущего документа на складе по услуги
// Возвращаемое значение:
//  - ТаблицаЗначений
Функция получитьУслугиДокумента()
    менеджерТаблиц = Документы.Реализация.ПолучитьУслугиДокумента(ЭтотОбъект.Ссылка, , Новый МенеджерВременныхТаблиц);

    запрос = Новый Запрос;
    запрос.МенеджерВременныхТаблиц = менеджерТаблиц;
    запрос.Текст =
        "ВЫБРАТЬ
        |	ВТ_УслугиДокумента.Номенклатура КАК Номенклатура,
        |	ВТ_УслугиДокумента.Номенклатура.СтатьяЗатрат КАК СтатьяЗатрат,
        |	ВТ_УслугиДокумента.Количество КАК Количество,
        |	ВТ_УслугиДокумента.Сумма КАК Сумма
        |ИЗ
        |   ВТ_УслугиДокумента КАК ВТ_УслугиДокумента
        |";

    результатЗапроса = запрос.Выполнить();
    Возврат результатЗапроса.Выгрузить();
КонецФункции

// Устанавливает блокировку дял `РегистрНакопления.ТоварыНаСкладах` по измерениям:
//  - `Номенклатура` - Со значениями из списка номенклатуры `ТЧ Товары` документа
//  - `Склад` - Если значение заполнено
// Возвращаемое значение:
//  - БлокировкаДанных
Функция получитьБлокировкуИзмененияТоварыНаСкладах()
    блокировка = Новый БлокировкаДанных;
    элементБлокировки = блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
    элементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
    элементБлокировки.ИсточникДанных = ЭтотОбъект.Товары;
    элементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");

    Если ЗначениеЗаполнено(ЭтотОбъект.Склад) Тогда
        элементБлокировки.УстановитьЗначение("Склад", ЭтотОбъект.Склад);
    КонецЕсли;

    Возврат блокировка;
КонецФункции
#КонецОбласти // ЗапросыДанных

#КонецОбласти // СлужебныеПроцедурыИФункции
