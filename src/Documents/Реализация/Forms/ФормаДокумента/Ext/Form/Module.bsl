#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(_)
    ткущаяСтрокаНоменклатуры = ЭтотОбъект.Элементы.Товары.ТекущиеДанные;
    обновитьЦенуНоменклатуры(ткущаяСтрокаНоменклатуры);
    обновитьСуммуПоТекущейСтрокеНоменклатуры(ткущаяСтрокаНоменклатуры);
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(_)
    ткущаяСтрокаНоменклатуры = ЭтотОбъект.Элементы.Товары.ТекущиеДанные;
    обновитьСуммуПоТекущейСтрокеНоменклатуры(ткущаяСтрокаНоменклатуры);
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(_)
    обновитьСуммуДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыТовары

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(_)
    ткущаяСтрокаНоменклатуры = ЭтотОбъект.Элементы.Услуги.ТекущиеДанные;
    обновитьЦенуНоменклатуры(ткущаяСтрокаНоменклатуры);
    обновитьСуммуПоТекущейСтрокеНоменклатуры(ткущаяСтрокаНоменклатуры);
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(_)
    ткущаяСтрокаНоменклатуры = ЭтотОбъект.Элементы.Услуги.ТекущиеДанные;
    обновитьСуммуПоТекущейСтрокеНоменклатуры(ткущаяСтрокаНоменклатуры);
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиПослеУдаления(_)
    обновитьСуммуДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыУслуги

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция получитьЦенуНоменклатуры(Знач номенклатура)
    Возврат получитьЦенуНоменклатурыНаСервере(номенклатура,
        ?(ЗначениеЗаполнено(ЭтотОбъект.Объект.Дата), ЭтотОбъект.Объект.Дата, Неопределено));
КонецФункции

&НаСервереБезКонтекста
Функция получитьЦенуНоменклатурыНаСервере(Знач номенклатураСсылка, Знач период)
    Возврат РегистрыСведений.ЦеныНоменклатуры.ПолучитьЦенуНоменклатуры(номенклатураСсылка, период);
КонецФункции

&НаКлиенте
Процедура обновитьЦенуНоменклатуры(Знач ткущаяСтрокаНоменклатуры)
    Если ЗначениеЗаполнено(ткущаяСтрокаНоменклатуры.Номенклатура) = Ложь Тогда
        ткущаяСтрокаНоменклатуры.Цена = 0;
        Возврат;
    КонецЕсли;

    ценаНоменклатуры = получитьЦенуНоменклатуры(ткущаяСтрокаНоменклатуры.Номенклатура);
    ткущаяСтрокаНоменклатуры.Цена = ?(ценаНоменклатуры = Неопределено, 0, ценаНоменклатуры);

    Если ценаНоменклатуры = Неопределено Тогда
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст = СтрШаблон(
                "Не удалось определить цену номенклатуры ""%1"".
                |Отсутствует информация в регистре цен номенклатуры по указанной позиции.", ткущаяСтрокаНоменклатуры.Номенклатура);
        сообщение.Сообщить();
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура обновитьСуммуПоТекущейСтрокеНоменклатуры(Знач ткущаяСтрокаНоменклатуры)
    ткущаяСтрокаНоменклатуры.Сумма = ткущаяСтрокаНоменклатуры.Цена * ткущаяСтрокаНоменклатуры.Количество;
КонецПроцедуры

&НаКлиенте
Процедура обновитьСуммуДокумента()
    ЭтотОбъект.Объект.СуммаДокумента = ЭтотОбъект.Объект.Товары.Итог("Сумма") + ЭтотОбъект.Объект.Услуги.Итог("Сумма");
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
