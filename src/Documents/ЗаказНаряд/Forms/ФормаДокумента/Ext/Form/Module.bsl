#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(_)
    обновитьСуммуПоТекущейСтрокеУслуг();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(_)
    ткущаяСтрокаУслуг = ЭтотОбъект.Элементы.Услуги.ТекущиеДанные;

    Если ЗначениеЗаполнено(ткущаяСтрокаУслуг.Номенклатура) = Ложь Тогда
        ткущаяСтрокаУслуг.Цена = 0;

    Иначе
        ценаНоменклатуры = получитьЦенуНоменклатуры(ткущаяСтрокаУслуг.Номенклатура);
        ткущаяСтрокаУслуг.Цена = ?(ценаНоменклатуры = Неопределено, 0, ценаНоменклатуры);

        Если ценаНоменклатуры = Неопределено Тогда
            сообщение = Новый СообщениеПользователю;
            сообщение.Текст = СтрШаблон(
                    "Не удалось определить цену номенклатуры ""%1"".
                    |Отсутствует информация в регистре цен номенклатуры по указанной позиции.", ткущаяСтрокаУслуг.Номенклатура);
            сообщение.Сообщить();
        КонецЕсли;
    КонецЕсли;

    обновитьСуммуПоТекущейСтрокеУслуг();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиПослеУдаления(_)
    обновитьСуммуДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыУслуги

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура обновитьСуммуПоТекущейСтрокеУслуг()
    ткущаяСтрокаУслуг = ЭтотОбъект.Элементы.Услуги.ТекущиеДанные;
    ткущаяСтрокаУслуг.Сумма = ткущаяСтрокаУслуг.Цена * ткущаяСтрокаУслуг.Количество;
КонецПроцедуры

&НаКлиенте
Процедура обновитьСуммуДокумента()
    ЭтотОбъект.Объект.СуммаДокумента = ЭтотОбъект.Объект.Услуги.Итог("Сумма");
КонецПроцедуры

&НаКлиенте
Функция получитьЦенуНоменклатуры(Знач номенклатура)
    Возврат получитьЦенуНоменклатурыНаСервере(номенклатура,
        ?(ЗначениеЗаполнено(ЭтотОбъект.Объект.Дата), ЭтотОбъект.Объект.Дата, Неопределено));
КонецФункции

&НаСервереБезКонтекста
Функция получитьЦенуНоменклатурыНаСервере(Знач номенклатураСсылка, Знач период)
    Возврат РегистрыСведений.ЦеныНоменклатуры.ПолучитьЦенуНоменклатуры(номенклатураСсылка, период);
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
