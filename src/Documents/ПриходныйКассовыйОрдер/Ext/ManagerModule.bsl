#Область ПрограммныйИнтерфейс

// Параметры:
//	видОперации - ПеречислениеСсылка.ВидыОперацийПоступленияДенег
//  значенияПолей - Структура, ФиксированнаяСтруктура
//      * Касса - СправочникСсылка.Кассы
//      * Плательщик - СправочникСсылка.Контрагенты, СправочникСсылка.Сотрудники
//      * Сумма - Число
// Возвращаемое значение:
//	- Структура
//      * СчетКредита - ПланСчетовСсылка.Хозрасчетный - [`РасчетыСПокупателями`, `РасчетыСПоставщиками`, `РасчетыСПодотчетнымиЛицами`]
//      * СубконтоДебет - СправочникСсылка.Кассы
//      * СубконтоКредит - СправочникСсылка.Контрагенты, СправочникСсылка.Сотрудники
//      * СодержаниеОперации - Строка
//      * Сумма - Число
Функция ПолучитьАналитикуПроводки(Знач видОперации, Знач значенияПолей) Экспорт
    структураАналитики = Новый Структура;
    структураАналитики.Вставить("СчетКредита", Неопределено);
    структураАналитики.Вставить("СубконтоДебет", значенияПолей.Касса);
    структураАналитики.Вставить("СубконтоКредит", Неопределено);
    структураАналитики.Вставить("СодержаниеОперации", Неопределено);
    структураАналитики.Вставить("Сумма", значенияПолей.Сумма);

    Если видОперации = Перечисления.ВидыОперацийПоступленияДенег.ОплатаОтПокупателя Тогда
        структураАналитики.СчетКредита = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
        структураАналитики.СубконтоКредит = значенияПолей.Плательщик;
        структураАналитики.СодержаниеОперации = "Оплата от покупателя наличными в кассу";

    ИначеЕсли видОперации = Перечисления.ВидыОперацийПоступленияДенег.ВозвратОтПоставщика Тогда
        структураАналитики.СчетКредита = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
        структураАналитики.СубконтоКредит = значенияПолей.Плательщик;
        структураАналитики.СодержаниеОперации = "Возврат от поставщика наличными в кассу";

    ИначеЕсли видОперации = Перечисления.ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника Тогда
        структураАналитики.СчетКредита = ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами;
        структураАналитики.СодержаниеОперации = "Возврат подотчетных средств";
        структураАналитики.СубконтоКредит = значенияПолей.Плательщик;

    Иначе
        ДиагностикаКлиентСервер.Утверждение(Ложь,
            СтрШаблон("Недопустимый вид операции: ""%1"".", Строка(видОперации)));
    КонецЕсли;

    Возврат структураАналитики;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
