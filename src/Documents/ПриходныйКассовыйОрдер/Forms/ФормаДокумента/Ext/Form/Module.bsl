#Область ОписаниеПеременных

&НаКлиенте
Перем _ВидыОперацийПоступленияДенег;

&НаКлиенте
Перем _ТипыКонтрагентов;

&НаКлиенте
Перем _Состояние;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(отказ)
    инициализация();

    поляИзменяющиеТипФормы = Новый Массив;
    поляИзменяющиеТипФормы.Добавить(Элементы.Плательщик);
    поляИзменяющиеТипФормы.Добавить(Элементы.ВидОперации);

    Для Каждого поле Из поляИзменяющиеТипФормы Цикл
        применитьНастройкиПолейФормы(Новый Структура("УправляющееПоле", поле));
    КонецЦикла;

КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидОперацииПриИзменении(_)
    изменитьТипФормы(Элементы.ВидОперации);
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикПриИзменении(_)
    изменитьТипФормы(Элементы.Плательщик);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область СлужебныеПроцедурыИФункции

// Изменяет тип отображения формы в зависимости от указанного значения управляющего поля формы
// Параметры:
//	управляющееПоле - ПолеФормы - управляющее поле формы из списка: [ВидОперации | Плательщик]
&НаКлиенте
Асинх Процедура изменитьТипФормы(управляющееПоле)
    имяУправляющегоРеквизита = управляющееПоле.Имя;

    Если НЕ проверитьДопустимоеУправляющееПоле(управляющееПоле) Тогда
        ВызватьИсключение "Недопустимое значение управляющего поля (реквизита) формы";
    КонецЕсли;

    поляДляОчистки = Новый Массив;

    параметрыОтображенияПолей = получитьНастройкиОтображенияПолей(Элементы[имяУправляющегоРеквизита]);
    Для Каждого поле Из параметрыОтображенияПолей.СкрываемыеПоля Цикл
        Если ЗначениеЗаполнено(поле.ТекстРедактирования) Тогда
            поляДляОчистки.Добавить(поле.Имя);
        КонецЕсли;
    КонецЦикла;

    текстВопроса = сформироватьТекстЗапросаОчисткиПолей(имяУправляющегоРеквизита, поляДляОчистки);
    применитьИзменения = Истина;

    Если поляДляОчистки.Количество() > 0 И ЗначениеЗаполнено(текстВопроса) Тогда
        кодВозврата = Ждать ВопросАсинх(текстВопроса, РежимДиалогаВопрос.ДаНет, , , "Внимание!");
        отменитьИзменение = кодВозврата <> КодВозвратаДиалога.Да;

        Если отменитьИзменение Тогда
            Объект[имяУправляющегоРеквизита] = _Состояние[имяУправляющегоРеквизита]; // Возврат значений полей (отмена очистки)
            применитьИзменения = Ложь;
        Иначе
            очиститьНеиспользуемыеРеквизитыФормы(поляДляОчистки);
        КонецЕсли;
    КонецЕсли;

    Если применитьИзменения Тогда
        применитьНастройкиПолейФормы(параметрыОтображенияПолей);
    КонецЕсли;

КонецПроцедуры

// Параметры:
//	настройкиПолей - Структура
&НаКлиенте
Процедура применитьНастройкиПолейФормы(настройкиПолей)
    Если настройкиПолей = Неопределено ИЛИ ТипЗнч(настройкиПолей) <> Тип("Структура") Тогда
        ВызватьИсключение "Недопустимое значение параметров настраиваемых полей формы";
    КонецЕсли;

    Если (НЕ настройкиПолей.Свойство("ЗначенияУстановлены")) И настройкиПолей.Свойство("УправляющееПоле") Тогда
        настройкиПолей = получитьНастройкиОтображенияПолей(настройкиПолей.УправляющееПоле);
    КонецЕсли;

    Для Каждого поле Из настройкиПолей.СкрываемыеПоля Цикл
        поле.Видимость = Ложь;
    КонецЦикла;

    Для Каждого поле Из настройкиПолей.ОтображаемыеПоля Цикл
        поле.Видимость = Истина;
    КонецЦикла;

    Если настройкиПолей.ТипПлательщика <> Неопределено И Элементы.Плательщик.Видимость Тогда
        массивОграничений = Новый Массив;
        массивОграничений.Добавить(настройкиПолей.ТипПлательщика);
        описаниеТипаПлательщика = Новый ОписаниеТипов(массивОграничений);
        Элементы.Плательщик.ОграничениеТипа = описаниеТипаПлательщика;

        Если настройкиПолей.ТипПлательщика = Тип("СправочникСсылка.Контрагенты") Тогда
            типКонтрагента = ?(ЭтотОбъект.Объект.ВидОперации =
                    _ВидыОперацийПоступленияДенег.ВозвратОтПоставщика,
                    _ТипыКонтрагентов.Поставщик,
                    _ТипыКонтрагентов.Покупатель);

            параметрыВыбораПлательщика = Новый Массив;
            параметрыВыбораПлательщика.Добавить(Новый ПараметрВыбора("Отбор.ТипКонтрагента", типКонтрагента));

            Элементы.Плательщик.ПараметрыВыбора = Новый ФиксированныйМассив(параметрыВыбораПлательщика);
        КонецЕсли;

    ИначеЕсли НЕ ЭтотОбъект.Элементы.Плательщик.Видимость Тогда
        Элементы.Плательщик.ОграничениеТипа = Новый ОписаниеТипов(Новый Массив);

    КонецЕсли;

    _Состояние[настройкиПолей.УправляющееПоле.Имя] = ЭтотОбъект.Объект[настройкиПолей.УправляющееПоле.Имя];
КонецПроцедуры

// Формирует строку вопроса для подтверждения очистки значений реквизитов
// Параметры:
//	имяУправляющегоПоля - Строка - имя управляющего поля (реквизита) при изменения которого выполняется очистка
//	поляДляОчистки - Массив - список имен реквизитов запрашиваемых для очистки
// Возвращаемое значение:
// Строка, Неопределено - текст вопроса, или Неопределено если список реквизитов пустой
&НаКлиенте
Функция сформироватьТекстЗапросаОчисткиПолей(Знач имяУправляющегоПоля, поляДляОчистки) // => Строка | Неопределено
    Если поляДляОчистки = Неопределено ИЛИ поляДляОчистки.Количество() = 0 Тогда
        Возврат Неопределено;
    КонецЕсли;

    текстВопроса = СтрШаблон("При изменении реквизита ""%1"" будут очищены следующие данные:", имяУправляющегоПоля);

    Для Каждого поле Из поляДляОчистки Цикл
        текстВопроса = текстВопроса + "
            | - %1";

        текстВопроса = СтрШаблон(текстВопроса, поле);
    КонецЦикла;

    Если ЗначениеЗаполнено(поляДляОчистки) Тогда
        текстВопроса = текстВопроса + "
            | Продолжить?";
    Иначе
        текстВопроса = Неопределено;
    КонецЕсли;

    Возврат текстВопроса;
КонецФункции

// Выполняет проверку является ли указанное имя реквизита именем управляющего пол: ВидОперации | Плательщик
// Параметры:
//	управляющееПоле - ПолеФормы
// Возвращаемое значение:
//	Булево - Истина если проверка успешна
&НаКлиенте
Функция проверитьДопустимоеУправляющееПоле(управляющееПоле)
    Если управляющееПоле = ЭтотОбъект.Элементы.ВидОперации
        ИЛИ управляющееПоле = ЭтотОбъект.Элементы.Плательщик Тогда

        Возврат Истина;
    КонецЕсли;

    Возврат Ложь;
КонецФункции

// Выполняет очистку ранее заполненных значений полей формы
// Параметры:
//	поляДляОчистки - Массив - Массив имен полей (реквизитов) для очистки
&НаКлиенте
Процедура очиститьНеиспользуемыеРеквизитыФормы(поляДляОчистки)
    Для Каждого имяРеквизита Из поляДляОчистки Цикл
        ЭтотОбъект.Объект[имяРеквизита] = Неопределено;
    КонецЦикла;
КонецПроцедуры

// Параметры:
//	управляющееПоле - ЭлементФормы - ВидОперации, Плательщик
// Возвращаемое значение:
//	- Структура, Неопределено
&НаКлиенте
Функция получитьНастройкиОтображенияПолей(управляющееПоле)
    Если управляющееПоле = ЭтотОбъект.Элементы.ВидОперации Тогда
        Возврат получитьСкрываемыеПоляПриИзмененииВидаОперации();

    ИначеЕсли управляющееПоле = ЭтотОбъект.Элементы.Плательщик Тогда
        Возврат получитьСкрываемыеПоляПриИзмененииПлательщика();

    Иначе ДиагностикаКлиентСервер.Утверждение(Ложь, "Недопустимое управляющее поле");
    КонецЕсли;

    Возврат Неопределено;
КонецФункции

// Определяет список скрываемых и отображаемых полей и тип значения поля Плательщик при изменении
// значения поля ВидаОперации
// Возвращаемое значение:
//	- Структура
&НаКлиенте
Функция получитьСкрываемыеПоляПриИзмененииВидаОперации()
    Если ЗначениеЗаполнено(ЭтотОбъект.Объект.ВидОперации) И (НЕ проверитьЭтоДопустимыйВидОперации(ЭтотОбъект.Объект.ВидОперации)) Тогда
        ВызватьИсключение "Недопустимый вид операции";
    КонецЕсли;

    скрываемыеПоля = Новый Структура;
    изменяемыеПоля = Новый Массив;
    типПлательщика = Неопределено;

    изменяемыеПоля.Добавить(ЭтотОбъект.Элементы.Договор);
    изменяемыеПоля.Добавить(ЭтотОбъект.Элементы.Плательщик);

    типПлательщика = определитьТипПлательщикаПоВидуОперации();

    Если ЗначениеЗаполнено(ЭтотОбъект.Объект.ВидОперации) Тогда
        Если НЕ этоВозвратОтПоставщика() Тогда
            скрываемыеПоля.Вставить(ЭтотОбъект.Элементы.Договор.Имя, ЭтотОбъект.Элементы.Договор);
        КонецЕсли;
    КонецЕсли;

    Возврат создатьОбъектПараметровНастраиваемогоПоля(ЭтотОбъект.Элементы.ВидОперации,
        скрываемыеПоля, изменяемыеПоля, типПлательщика);
КонецФункции

// Определяет список скрываемых и отображаемых полей при изменении значения управляющего поля Плательщик
// Возвращаемое значение:
//	- Структура
&НаКлиенте
Функция получитьСкрываемыеПоляПриИзмененииПлательщика()
    скрываемыеПоля = Новый Структура;
    изменяемыеПоля = Новый Массив;

    изменяемыеПоля.Добавить(ЭтотОбъект.Элементы.Договор);

    Если ЗначениеЗаполнено(ЭтотОбъект.Объект.Плательщик)
        И этотКонтрагентПоставщик(ЭтотОбъект.Объект.Плательщик) = Ложь Тогда

        скрываемыеПоля.Вставить(ЭтотОбъект.Элементы.Договор.Имя, ЭтотОбъект.Элементы.Договор);
    КонецЕсли;

    Возврат создатьОбъектПараметровНастраиваемогоПоля(ЭтотОбъект.Элементы.Плательщик, скрываемыеПоля, изменяемыеПоля);
КонецФункции

// Создает объект параметров настраиваемых полей формы при указанном значении управляющего поля
// Параметры:
//	управляющееПоле - ПолеФормы - управляющее поле формы: ВидОперации | Плательщик
//	скрываемыеПоля - Структура - список имен и значений скрываемых полей вида: [{Ключ: Строка -> ИмяПоляФормы, Значение: ПолеФормы}]
//	изменяемыеПоля - Массив - список всех полей формы настраиваемых управляющим полем
//	типПлательщика - Тип, Неопределено - Тип значения поля Плательщик при выбранном значении управляющего поля
// Возвращаемое значение:
//	- Структура - объект параметров отображения настраиваемого поля
&НаКлиенте
Функция создатьОбъектПараметровНастраиваемогоПоля(управляющееПоле, скрываемыеПоля, изменяемыеПоля, типПлательщика = Неопределено)
    отображаемыеПоля = Новый Массив;
    Для Каждого поле Из изменяемыеПоля Цикл
        Если НЕ (скрываемыеПоля.Свойство(поле.Имя) ИЛИ поле.Видимость) Тогда
            отображаемыеПоля.Добавить(поле);
        КонецЕсли;
    КонецЦикла;

    скрываемыеПоляМассив = Новый Массив;
    Для Каждого элемент Из скрываемыеПоля Цикл
        Если элемент.Значение.Видимость Тогда
            скрываемыеПоляМассив.Добавить(элемент.Значение);
        КонецЕсли;
    КонецЦикла;

    результат = Новый Структура;
    результат.Вставить("ОтображаемыеПоля", отображаемыеПоля);
    результат.Вставить("УправляющееПоле", управляющееПоле);
    результат.Вставить("СкрываемыеПоля", скрываемыеПоляМассив);
    результат.Вставить("ТипПлательщика", типПлательщика);
    результат.Вставить("ЗначенияУстановлены", Истина);

    Возврат результат;
КонецФункции

// Возвращаемое значение:
//	- Тип, Неопределено
&НаКлиенте
Функция определитьТипПлательщикаПоВидуОперации()
    типПлательщика = Неопределено;

    Если этоВозвратОтПодотчетника() Тогда
        типПлательщика = Тип("СправочникСсылка.Сотрудники");

    Иначе
        типПлательщика = Тип("СправочникСсылка.Контрагенты");

    КонецЕсли;

    Возврат типПлательщика;
КонецФункции

&НаКлиенте
Функция проверитьЭтоДопустимоеЗначениеУправляющегоПоля(Знач значение, допустимыеЗначения)
    результат = Ложь;
    Для Каждого элемент Из допустимыеЗначения Цикл
        результат = элемент.Значение = значение;
        Если результат Тогда
            Прервать;
        КонецЕсли;
    КонецЦикла;

    Возврат результат;
КонецФункции

&НаКлиенте
Функция проверитьЭтоДопустимыйВидОперации(Знач видОперации)
    Возврат проверитьЭтоДопустимоеЗначениеУправляющегоПоля(видОперации, _ВидыОперацийПоступленияДенег);
КонецФункции

#Область СлужебныеПоля
// Параметры:
//	плательщикСсылка - СправочникСсылка.Контрагенты, Произвольный
// Возвращаемое значение:
//	- Булево
&НаСервереБезКонтекста
Функция этотКонтрагентКлиент(Знач плательщикСсылка)
    Возврат Справочники.Контрагенты.ЭтотКонтрагентКлиент(плательщикСсылка);
КонецФункции

// Параметры:
//	плательщикСсылка - СправочникСсылка.Контрагенты, Произвольный
// Возвращаемое значение:
//	- Булево
&НаСервереБезКонтекста
Функция этотКонтрагентПоставщик(Знач плательщикСсылка)
    Возврат Справочники.Контрагенты.ЭтотКонтрагентПоставщик(плательщикСсылка);
КонецФункции

// Параметры:
//	плательщикСсылка - СправочникСсылка.Банки, Произвольный
//
// Возвращаемое значение:
//	- Булево
&НаКлиенте
Функция этотПлательщикБанк(Знач плательщикСсылка)
    Возврат ТипЗнч(плательщикСсылка) = Тип("СправочникСсылка.Банки");
КонецФункции

// Параметры:
//	плательщикСсылка - СправочникСсылка.Сотрудники, Произвольный
//
// Возвращаемое значение:
//	- Булево
&НаКлиенте
Функция этотПлательщикСотрудник(Знач плательщикСсылка)
    Возврат ТипЗнч(плательщикСсылка) = Тип("СправочникСсылка.Сотрудники");
КонецФункции

// Возвращаемое значение:
//	- Булево
&НаКлиенте
Функция этоВозвратОтПодотчетника()
    Возврат ЭтотОбъект.Объект.ВидОперации = _ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника;
КонецФункции

// Возвращаемое значение:
//	- Булево
&НаКлиенте
Функция этоВозвратОтПоставщика()
    Возврат ЭтотОбъект.Объект.ВидОперации = _ВидыОперацийПоступленияДенег.ВозвратОтПоставщика;
КонецФункции

// Возвращаемое значение:
//	- Булево
&НаКлиенте
Функция этоОплатаОтПокупателя()
    Возврат ЭтотОбъект.Объект.ВидОперации = _ВидыОперацийПоступленияДенег.ОплатаОтПокупателя;
КонецФункции

#КонецОбласти // СлужебныеПоля

#КонецОбласти // СлужебныеПроцедурыИФункции

#Область Инициализация

&НаКлиенте
Процедура инициализация()
    значенияПеречислений = получитьЗначенияПеречислений();
    _ВидыОперацийПоступленияДенег = значенияПеречислений.ВидыОперацийПоступленияДенег;
    _ТипыКонтрагентов = значенияПеречислений.ТипыКонтрагентов;

    Если ЭтотОбъект.Объект.Ссылка.Пустая() Тогда
        заполнитьПоляФормыЗначениямиПоУмолчанию(НЕ ЗначениеЗаполнено(ЭтотОбъект.Объект.ДокументОснование));
    КонецЕсли;

    _Состояние = Новый Структура("ВидОперации, Плательщик");
    _Состояние.ВидОперации = ЭтотОбъект.Объект.ВидОперации;
    _Состояние.Плательщик = ЭтотОбъект.Объект.Плательщик;
КонецПроцедуры

&НаКлиенте
Процедура заполнитьПоляФормыЗначениямиПоУмолчанию(Знач принудительно = Ложь)
    ЭтотОбъект.Объект.ВидОперации = ?(НЕ принудительно И ЗначениеЗаполнено(ЭтотОбъект.Объект.ВидОперации),
            ЭтотОбъект.Объект.ВидОперации, _ВидыОперацийПоступленияДенег.ОплатаОтПокупателя);
КонецПроцедуры

// Возвращаемое значение:
//	- ФиксированнаяСтруктура
//      * ВидыОперацийПоступленияДенег - Структура
&НаСервереБезКонтекста
Функция получитьЗначенияПеречислений()
    результат = Новый Структура;
    результат.Вставить("ВидыОперацийПоступленияДенег", РаботаСМетаданными.ПолучитьЗначенияПеречисления(
            Тип("ПеречислениеСсылка.ВидыОперацийПоступленияДенег")));
    результат.Вставить("ТипыКонтрагентов", РаботаСМетаданными.ПолучитьЗначенияПеречисления(
            Тип("ПеречислениеСсылка.ТипыКонтрагентов")));

    Возврат Новый ФиксированнаяСтруктура(результат);
КонецФункции

#КонецОбласти // Инициализация
