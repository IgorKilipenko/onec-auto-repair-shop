#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(_, __, ___)
    Если ЗначениеЗаполнено(ЭтотОбъект.АвторДокумента) = Ложь Тогда
        ЭтотОбъект.АвторДокумента = ПараметрыСеанса.ТекущийПользователь;
    КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(_, __)
    выполнитьВсеДвижения();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

#Область Движения
Процедура выполнитьВсеДвижения()
    ЭтотОбъект.Движения.Начисления.Записывать = Истина;
    ЭтотОбъект.Движения.Хозрасчетный.Записывать = Истина;
    ЭтотОбъект.Движения.УчетЗатрат.Записывать = Истина;

    выполнитьДвиженияПоВсемНачислениям();
    ЭтотОбъект.Движения.Начисления.Записать();
    ЭтотОбъект.Движения.Начисления.РассчитатьСуммуНачисления();

    данныеНачислений = РегистрыРасчета.Начисления.ПолучитьДанныеНачисленийДокументаРегистратора(ЭтотОбъект.Ссылка, Истина);

    Для Каждого статьяЗатратНачисления Из данныеНачислений.НачисленияПоСтатьямЗатрат Цикл
        выполнитьДвижениеУчетЗатратОборот(статьяЗатратНачисления.СтатьяЗатрат, статьяЗатратНачисления.Сумма);
    КонецЦикла;

    Для Каждого структураНачисления Из данныеНачислений.Начисления Цикл
        выполнитьДвижениеНачислениеЗарплаты(структураНачисления);
    КонецЦикла;
КонецПроцедуры

Процедура выполнитьДвиженияПоВсемНачислениям()
    Для Каждого строкаНачисления Из ЭтотОбъект.Начисления Цикл
        структураНачисления = РегистрыРасчета.Начисления.СоздатьПустуюСтруктуруНачисления();
        ЗаполнитьЗначенияСвойств(структураНачисления, строкаНачисления);

        ЭтотОбъект.Движения.Начисления.ДобавитьНачисление(ЭтотОбъект.ПериодНачисления, структураНачисления);
    КонецЦикла;
КонецПроцедуры

Процедура выполнитьДвижениеНачислениеЗарплаты(Знач структураНачисления)
    ЭтотОбъект.Движения.Хозрасчетный.ДобавитьЗаписьРасчетаСПерсоналом(
        ЭтотОбъект.Дата, Новый ФиксированнаяСтруктура(
            "Сотрудник, СтатьяЗатрат, Сумма, СодержаниеОперации",
            структураНачисления.Сотрудник,
            структураНачисления.СтатьяЗатрат,
            структураНачисления.Сумма,
            "Отражено начисление заработной платы сотрудникам"));
КонецПроцедуры

Процедура выполнитьДвижениеУчетЗатратОборот(Знач статьяЗатрат, Знач сумма)
    движение = Движения.УчетЗатрат.Добавить();
    движение.Период = ЭтотОбъект.Дата;
    движение.СтатьяЗатрат = статьяЗатрат;
    движение.Сумма = сумма;
КонецПроцедуры
#КонецОбласти // Движения

#КонецОбласти // СлужебныеПроцедурыИФункции
