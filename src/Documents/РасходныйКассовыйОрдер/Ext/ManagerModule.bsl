#Область СлужебныйПрограммныйИнтерфейс

// Формирует структуру аналитики проводки для заполнения движения по `РегистрыБухгалтерии.Хозрасчетный`
//  для отражения операции расхода денег из кассы.
//  Не заполняет поле `СчетКредита`, счет кредита должен быть установлен при создании движения.
// Параметры:
//	видОперации - ПеречислениеСсылка.ВидыОперацийПоступленияДенег
//  значенияПолей - Структура, ФиксированнаяСтруктура
//      * Касса - СправочникСсылка.Кассы
//      * Получатель - СправочникСсылка.Контрагенты, СправочникСсылка.Сотрудники
//      * Сумма - Число
// Возвращаемое значение:
//	- Структура
//      * СчетДебета - ПланСчетовСсылка.Хозрасчетный - [`РасчетыСПоставщиками`, `РасчетыСПокупателями`, `РасчетыСПерсоналомПоОплатеТруда`, `РасчетыСПодотчетнымиЛицами`]
//      * СубконтоКредит - СправочникСсылка.Кассы
//      * СубконтоДебет - СправочникСсылка.Контрагенты, СправочникСсылка.Сотрудники
//      * СодержаниеОперации - Строка
//      * Сумма - Число
Функция ПолучитьАналитикуПроводки(Знач видОперации, Знач значенияПолей) Экспорт
    структураАналитики = Новый Структура;
    структураАналитики.Вставить("СчетДебета", Неопределено);
    структураАналитики.Вставить("СубконтоДебет", значенияПолей.Получатель);
    структураАналитики.Вставить("СубконтоКредит", значенияПолей.Касса);
    структураАналитики.Вставить("СодержаниеОперации", Неопределено);
    структураАналитики.Вставить("Сумма", значенияПолей.Сумма);

    структураАналитики.СубконтоДебет = значенияПолей.Получатель;

    Если видОперации = Перечисления.ВидыОперацийРасходаДенег.ОплатаПоставщику Тогда
        // Оплата поставщику
        структураАналитики.СчетДебета = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
        структураАналитики.СодержаниеОперации = "Оплата поставщику наличными из кассы";

    ИначеЕсли видОперации = Перечисления.ВидыОперацийРасходаДенег.ВозвратПокупателю Тогда
        // Возврат денег покупателю
        структураАналитики.СчетДебета = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
        структураАналитики.СодержаниеОперации = "Возврат покупателю наличными из кассы";

    ИначеЕсли видОперации = Перечисления.ВидыОперацийРасходаДенег.ВыдачаЗаработнойПлаты Тогда
        // Выплата заработной платы
        структураАналитики.СчетДебета = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
        структураАналитики.СодержаниеОперации = "Выплата заработной платы наличными из кассы";

    ИначеЕсли видОперации = Перечисления.ВидыОперацийРасходаДенег.ВыдачаПодотчетнику Тогда
        // Выдача денег подотчетнику
        структураАналитики.СчетДебета = ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами;
        структураАналитики.СодержаниеОперации = "Выдача денежных средств подотчётному лицу";

    Иначе
        ДиагностикаКлиентСервер.Утверждение(Ложь, "Недопустимый вид операции.");
    КонецЕсли;

    Возврат структураАналитики;
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
