#Область СлужебныеПроцедурыИФункции

#Область User
#Область GET
Функция User_ПолучитьПользователя(запрос)
    телоОтвета = Новый Структура("Успех, Пользователь, Ошибка", Истина);
    ответ = Новый HTTPСервисОтвет(200);
    ответ.Заголовки.Вставить("Content-type", "application/json");

    имяПользователя = запрос.ПараметрыЗапроса.Получить("name");
    Если НЕ (ТипЗнч(имяПользователя) = Тип("Строка") И НЕ ПустаяСтрока(имяПользователя)) Тогда
        ответ.КодСостояния = 400;
        телоОтвета.Ошибка = "Не указано имя пользователя.";
        ответ.УстановитьТелоИзСтроки(ЗаписатьЗначениеJSON(телоОтвета));

        Возврат ответ;
    КонецЕсли;

    пользовательПриложения = Неопределено;
    результатПоискаПользователя = Справочники.Пользователи.НайтиПользователяПоИмени(имяПользователя);
    Если результатПоискаПользователя <> Неопределено Тогда
        имяПользователя = РаботаСРеквизитами.ЗначенияРеквизитовОбъекта(
                результатПоискаПользователя.Ссылка, "Наименование").Наименование;
        пользовательПриложения = Новый Структура("Имя", имяПользователя);
        пользовательПриложения.Вставить("УникальныйИдентификатор",
            Строка(результатПоискаПользователя.Ссылка.УникальныйИдентификатор()));
    КонецЕсли;

    телоОтвета.Пользователь = пользовательПриложения;
    ответ.УстановитьТелоИзСтроки(ЗаписатьЗначениеJSON(телоОтвета));

    Возврат ответ;
КонецФункции
#КонецОбласти // GET
#КонецОбласти // User

#КонецОбласти // СлужебныеПроцедурыИФункции
