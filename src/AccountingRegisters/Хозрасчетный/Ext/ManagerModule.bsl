#Область ПрограммныйИнтерфейс

// Устарела.
Функция ЗаполнитьДвижениеРасчетыСПерсоналом(Знач движение, Знач дата, Знач структураНачисления, Знач содержание) Экспорт
    движение.СчетДт = ПланыСчетов.Хозрасчетный.РасходыНаПродажу;
    движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
    движение.Период = дата;
    движение.Сумма = структураНачисления.Сумма;
    движение.Содержание = содержание;
    заполнитьСубконтоПоСчету(движение.СчетДт, движение.СубконтоДт, структураНачисления.СтатьяЗатрат);
    заполнитьСубконтоПоСчету(движение.СчетКт, движение.СубконтоКт, структураНачисления.Сотрудник);

    Возврат движение;
КонецФункции

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для учета поступления услуг.
//  * Счет дебета: `ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками`
// Параметры:
//  дата - Дата - Дата записи
//  наборЗаписей - РегистрБухгалтерииНаборЗаписей.Хозрасчетный - Набор записей
//  строкаУслуг - Структура, ФиксированнаяСтруктура
//      * СчетБухгалтерскогоУчета - ПланСчетовСсылка.Хозрасчетный - Счет дебита
//      * СтатьяЗатрат - СправочникСсылка.СтатьиЗатрат - Отражается в субконто дебета
//      * Сумма - Число
//  поставщик - СправочникСсылка.Контрагенты - отражается в субконто кредита
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись - Заполненная запись
Функция ДобавитьЗаписьПоступлениеУслуг(Знач дата, Знач наборЗаписей, Знач строкаУслуг, Знач поставщик) Экспорт
    записьБУ = наборЗаписей.Добавить();
    записьБУ.Период = дата;
    записьБУ.СчетДт = строкаУслуг.СчетБухгалтерскогоУчета;
    записьБУ.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
    записьБУ.Сумма = строкаУслуг.Сумма;
    записьБУ.Содержание = ПолучитьСодержаниеДляПоступленияУслуг();
    заполнитьСубконтоПоСчету(записьБУ.СчетДт, записьБУ.СубконтоДт, строкаУслуг.СтатьяЗатрат);
    заполнитьСубконтоПоСчету(записьБУ.СчетКт, записьБУ.СубконтоКт, поставщик);

    Возврат записьБУ;
КонецФункции

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для учета поступления товаров и материалов
//  * Счет дебета: `ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками`
//  * Счет кредита: `ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками`
// Параметры:
//  дата - Дата - Дата записи
//  наборЗаписей - РегистрБухгалтерииНаборЗаписей.Хозрасчетный - Набор записей
//  строкаТоваров - Структура, ФиксированнаяСтруктура, СтрокаТаблицыЗначений
//      * Номенклатура - СправочникСсылка.Номенклатура - отражается в субконто дебета
//      * СчетБухгалтерскогоУчета - ПланСчетовСсылка.Хозрасчетный - Счет дебета
//      * Сумма - Число
//  поставщик - СправочникСсылка.Контрагенты - отражается в субконто кредита
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись - Заполненная запись
Функция ДобавитьЗаписьПоступлениеТМЦ(Знач дата, Знач наборЗаписей, Знач строкаТоваров, Знач поставщик) Экспорт
    записьБУ = наборЗаписей.Добавить();
    записьБУ.Период = дата;
    записьБУ.СчетДт = строкаТоваров.СчетБухгалтерскогоУчета;
    записьБУ.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
    записьБУ.Сумма = строкаТоваров.Сумма;
    записьБУ.Содержание = "Отражено поступление товарно-материальных ценностей от поставщика";
    заполнитьСубконтоПоСчету(записьБУ.СчетДт, записьБУ.СубконтоДт, строкаТоваров.Номенклатура);
    заполнитьСубконтоПоСчету(записьБУ.СчетКт, записьБУ.СубконтоКт, поставщик);

    Возврат записьБУ;
КонецФункции

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для отражения себестоимости товаров и материалов.
//  * Счет дебета: `ПланыСчетов.Хозрасчетный.Продажи`
//  * Счет кредита: Принимается из аргумента `структураСписания.СчетКредита`
// Параметры:
//  дата - Дата - Дата записи
//  наборЗаписей - РегистрБухгалтерииНаборЗаписей.Хозрасчетный - Набор записей
//  структураСписания - Структура, ФиксированнаяСтруктура, СтрокаТаблицыЗначений
//      * Номенклатура - СправочникСсылка.Номенклатура - отражается в субконто кредита
//      * СчетКредита - ПланСчетовСсылка.Хозрасчетный - Счет кредита, `10` для материалов, `41` для товаров
//      * Сумма - Число - Сумма списания
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись - Заполненная запись
Функция ДобавитьЗаписьСписанияСебестоимостиТМЦ(Знач дата, Знач наборЗаписей, Знач структураСписания) Экспорт
    записьБУ = наборЗаписей.Добавить();
    записьБУ.Период = дата;
    записьБУ.СчетДт = ПланыСчетов.Хозрасчетный.Продажи;
    записьБУ.СчетКт = структураСписания.СчетКредита;
    записьБУ.Сумма = структураСписания.Сумма;
    записьБУ.Содержание = "Списана себестоимость товарно-материальных ценностей";
    заполнитьСубконтоПоСчету(записьБУ.СчетКт, записьБУ.СубконтоКт, структураСписания.Номенклатура);

    Возврат записьБУ;
КонецФункции

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для отражения выручки от реализации товаров и материалов
//  * Счет дебета: `ПланыСчетов.Хозрасчетный.РасчетыСПокупателями`
//  * Счет кредита: `ПланыСчетов.Хозрасчетный.Продажи`
// Параметры:
//  дата - Дата - Дата записи
//  наборЗаписей - РегистрБухгалтерииНаборЗаписей.Хозрасчетный - Набор записей
//  структураПродаж - Структура, ФиксированнаяСтруктура, СтрокаТаблицыЗначений
//      * Клиент - СправочникСсылка.Контрагенты - отражается в субконто дебета
//      * Сумма - Число - Стоимость продаж номенклатуры документа
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись - Заполненная запись
Функция ДобавитьЗаписьОтраженияВыручкиОтРеализацииТМЦ(Знач дата, Знач наборЗаписей, Знач структураПродаж) Экспорт
    движение = наборЗаписей.Добавить();
    движение.Период = дата;
    движение.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
    движение.СчетКт = ПланыСчетов.Хозрасчетный.Продажи;
    движение.Сумма = структураПродаж.Сумма;
    движение.Содержание = "Отражена выручка от реализации товаров и услуг";
    заполнитьСубконтоПоСчету(движение.СчетДт, движение.СубконтоДт, структураПродаж.Клиент);

    Возврат движение;
КонецФункции

// Возвращаемое значение:
//  - Строка - Содержание по умолчанию для поступления услуг
Функция ПолучитьСодержаниеДляПоступленияУслуг() Экспорт
    Возврат "Отражено поступление услуг от поставщика";
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

#Область ПомощникиПроводок
// Параметры:
//  счет - ПланСчетовСсылка.Хозрасчетный
// Возвращаемое значение:
//  - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные
Функция получитьВидСубконтоПоСчету(Знач счет)
    видыСубконто = счет.ВидыСубконто;

    Если видыСубконто.Количество() = 0 Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат видыСубконто[0].ВидСубконто;
КонецФункции

// Параметры:
//  счет - ПланСчетовСсылка.Хозрасчетный
//  субконто - РегистрБухгалтерииСубконто
//  значение - СправочникСсылка
// Возвращаемое значение:
//  - Булево
Функция заполнитьСубконтоПоСчету(Знач счет, субконто, Знач значение) Экспорт
    видСубконто = получитьВидСубконтоПоСчету(счет);
    Если видСубконто = Неопределено Тогда
        Возврат Ложь;
    КонецЕсли;

    субконто[видСубконто] = значение;
    Возврат Истина;
КонецФункции
#КонецОбласти // ПомощникиПроводок

#КонецОбласти // СлужебныеПроцедурыИФункции
