#Область ПрограммныйИнтерфейс

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для учета поступления услуг.
//  * Счет дебета: Устанавливается из аргумента `строкаУслуг.СчетБухгалтерскогоУчета`
//  * Счет кредита: `ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками`
// Параметры:
//  дата - Дата - Дата записи
//  строкаУслуг - Структура, ФиксированнаяСтруктура
//      * СчетБухгалтерскогоУчета - ПланСчетовСсылка.Хозрасчетный - Счет дебита
//      * СтатьяЗатрат - СправочникСсылка.СтатьиЗатрат - Отражается в субконто дебета
//      * Сумма - Число
//  поставщик - СправочникСсылка.Контрагенты - отражается в субконто кредита
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись - Заполненная запись
Функция ДобавитьЗаписьПоступлениеУслуг(Знач дата, Знач строкаУслуг, Знач поставщик) Экспорт
    записьБУ = ЭтотОбъект.Добавить();
    записьБУ.Период = дата;
    записьБУ.СчетДт = строкаУслуг.СчетБухгалтерскогоУчета;
    записьБУ.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
    записьБУ.Сумма = строкаУслуг.Сумма;
    записьБУ.Содержание = "Отражено поступление услуг от поставщика";

    // Заполнение субконто дебета
    субконтоЗаполнен = Ложь;
    субконтоЗаполнен = заполнитьСубконтоПоСчету(записьБУ.СчетДт, записьБУ.СубконтоДт, строкаУслуг.СтатьяЗатрат);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен);

    // Заполнение субконто кредита
    субконтоЗаполнен = заполнитьСубконтоПоСчету(записьБУ.СчетКт, записьБУ.СубконтоКт, поставщик);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен);

    Возврат записьБУ;
КонецФункции

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для учета поступления товаров и материалов
//  * Счет дебета: - Устанавливается из аргумента `строкаТоваров.СчетБухгалтерскогоУчета`
//  * Счет кредита: `ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками`
// Параметры:
//  дата - Дата - Дата записи
//  строкаТоваров - Структура, ФиксированнаяСтруктура, СтрокаТаблицыЗначений
//      * Номенклатура - СправочникСсылка.Номенклатура - отражается в субконто дебета
//      * СчетБухгалтерскогоУчета - ПланСчетовСсылка.Хозрасчетный - Счет дебета
//      * Сумма - Число
//  поставщик - СправочникСсылка.Контрагенты - отражается в субконто кредита
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись - Заполненная запись
Функция ДобавитьЗаписьПоступлениеТМЦ(Знач дата, Знач строкаТоваров, Знач поставщик) Экспорт
    записьБУ = ЭтотОбъект.Добавить();
    записьБУ.Период = дата;
    записьБУ.СчетДт = строкаТоваров.СчетБухгалтерскогоУчета;
    записьБУ.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
    записьБУ.Сумма = строкаТоваров.Сумма;
    записьБУ.Содержание = "Отражено поступление товарно-материальных ценностей от поставщика";

    // Заполнение субконто дебета
    субконтоЗаполнен = Ложь;
    субконтоЗаполнен = заполнитьСубконтоПоСчету(записьБУ.СчетДт, записьБУ.СубконтоДт, строкаТоваров.Номенклатура);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен);

    // Заполнение субконто кредита
    субконтоЗаполнен = заполнитьСубконтоПоСчету(записьБУ.СчетКт, записьБУ.СубконтоКт, поставщик);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен);

    Возврат записьБУ;
КонецФункции

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для отражения себестоимости товаров и материалов.
//  * Счет дебета: `ПланыСчетов.Хозрасчетный.Продажи`
//  * Счет кредита: Устанавливается из аргумента `структураСписания.СчетКредита`
// Параметры:
//  дата - Дата - Дата записи
//  структураСписания - Структура, ФиксированнаяСтруктура, СтрокаТаблицыЗначений
//      * Номенклатура - СправочникСсылка.Номенклатура - отражается в субконто кредита
//      * СчетКредита - ПланСчетовСсылка.Хозрасчетный - Счет кредита, `10` для материалов, `41` для товаров
//      * Сумма - Число - Сумма списания
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись - Заполненная запись
Функция ДобавитьЗаписьСписанияСебестоимостиТМЦ(Знач дата, Знач структураСписания) Экспорт
    записьБУ = ЭтотОбъект.Добавить();
    записьБУ.Период = дата;
    записьБУ.СчетДт = ПланыСчетов.Хозрасчетный.Продажи;
    записьБУ.СчетКт = структураСписания.СчетКредита;
    записьБУ.Сумма = структураСписания.Сумма;
    записьБУ.Содержание = "Списана себестоимость товарно-материальных ценностей";

    // Заполнение субконто кредита
    субконтоЗаполнен = Ложь;
    субконтоЗаполнен = заполнитьСубконтоПоСчету(записьБУ.СчетКт, записьБУ.СубконтоКт, структураСписания.Номенклатура);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен);

    Возврат записьБУ;
КонецФункции

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для отражения выручки от реализации товаров и материалов
//  * Счет дебета: `ПланыСчетов.Хозрасчетный.РасчетыСПокупателями`
//  * Счет кредита: `ПланыСчетов.Хозрасчетный.Продажи`
// Параметры:
//  дата - Дата - Дата записи
//  структураПродаж - Структура, ФиксированнаяСтруктура, СтрокаТаблицыЗначений
//      * Клиент - СправочникСсылка.Контрагенты - отражается в субконто дебета
//      * Сумма - Число - Стоимость продаж номенклатуры документа
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись - Заполненная запись
Функция ДобавитьЗаписьОтраженияВыручкиОтРеализацииТМЦ(Знач дата, Знач структураПродаж) Экспорт
    записьБУ = ЭтотОбъект.Добавить();
    записьБУ.Период = дата;
    записьБУ.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
    записьБУ.СчетКт = ПланыСчетов.Хозрасчетный.Продажи;
    записьБУ.Сумма = структураПродаж.Сумма;
    записьБУ.Содержание = "Отражена выручка от реализации товаров и услуг";

    // Заполнение субконто дебета
    субконтоЗаполнен = Ложь;
    субконтоЗаполнен = заполнитьСубконтоПоСчету(записьБУ.СчетДт, записьБУ.СубконтоДт, структураПродаж.Клиент);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен);

    Возврат записьБУ;
КонецФункции

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для отражения поступления денег в кассу
//  * Счет дебета: `ПланыСчетов.Хозрасчетный.Касса`
//  * Счет кредита: Устанавливается из аргумента `аналитикаПроводки.СчетКредита`
// Параметры:
//  дата - Дата - Дата записи
//  аналитикаПроводки - Структура, ФиксированнаяСтруктура
//      * СчетКредита - ПланСчетовСсылка.Хозрасчетный - [`РасчетыСПокупателями`, `РасчетыСПоставщиками`, `РасчетыСПодотчетнымиЛицами`]
//      * СубконтоДебет - СправочникСсылка.Кассы
//      * СубконтоКредит - СправочникСсылка.Контрагенты, СправочникСсылка.Сотрудники
//      * СодержаниеОперации - Строка
//      * Сумма - Число
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись.Хозрасчетный - Заполненная запись
Функция ДобавитьЗаписьПоступлениеДенегВКассу(Знач дата, Знач аналитикаПроводки) Экспорт
    записьБУ = ЭтотОбъект.Добавить();
    записьБУ.Период = дата;
    записьБУ.СчетДт = ПланыСчетов.Хозрасчетный.Касса;
    записьБУ.СчетКт = аналитикаПроводки.СчетКредита;
    записьБУ.Сумма = аналитикаПроводки.Сумма;
    записьБУ.Содержание = аналитикаПроводки.СодержаниеОперации;

    // Заполнение субконто дебета
    субконтоЗаполнен = Ложь;
    субконтоЗаполнен = заполнитьСубконтоПоСчету(записьБУ.СчетДт, записьБУ.СубконтоДт, аналитикаПроводки.СубконтоДебет);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен);

    // Заполнение субконто кредита
    субконтоЗаполнен = заполнитьСубконтоПоСчету(записьБУ.СчетКт, записьБУ.СубконтоКт, аналитикаПроводки.СубконтоКредит);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен);

    Возврат записьБУ;
КонецФункции

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для отражения расход денег из кассы
//  * Счет дебета: Устанавливается из аргумента `аналитикаПроводки.СчетДебета`
//  * Счет кредита: `ПланыСчетов.Хозрасчетный.Касса`
// Параметры:
//  дата - Дата - Дата записи
//  аналитикаПроводки - Структура, ФиксированнаяСтруктура
//      * СчетДебета - ПланСчетовСсылка.Хозрасчетный - [`РасчетыСПоставщиками`, `РасчетыСПокупателями`, `РасчетыСПерсоналомПоОплатеТруда`, `РасчетыСПодотчетнымиЛицами`]
//      * СубконтоКредит - СправочникСсылка.Кассы
//      * СубконтоДебет - СправочникСсылка.Контрагенты, СправочникСсылка.Сотрудники
//      * СодержаниеОперации - Строка
//      * Сумма - Число
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись.Хозрасчетный - Заполненная запись
Функция ДобавитьЗаписьРасходаДенегИзКассы(Знач дата, Знач аналитикаПроводки) Экспорт
    записьБУ = ЭтотОбъект.Добавить();
    записьБУ.Период = дата;
    записьБУ.СчетДт = аналитикаПроводки.СчетДебета;
    записьБУ.СчетКт = ПланыСчетов.Хозрасчетный.Касса;
    записьБУ.Сумма = аналитикаПроводки.Сумма;
    записьБУ.Содержание = аналитикаПроводки.СодержаниеОперации;

    // Заполнение субконто дебета
    субконтоЗаполнен = Ложь;
    субконтоЗаполнен = заполнитьСубконтоПоСчету(записьБУ.СчетДт, записьБУ.СубконтоДт, аналитикаПроводки.СубконтоДебет);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен);

    // Заполнение субконто кредита
    субконтоЗаполнен = заполнитьСубконтоПоСчету(записьБУ.СчетКт, записьБУ.СубконтоКт, аналитикаПроводки.СубконтоКредит);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен);

    Возврат записьБУ;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

#Область ПомощникиПроводок
// Параметры:
//  счет - ПланСчетовСсылка.Хозрасчетный
// Возвращаемое значение:
//  - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные
Функция получитьВидСубконтоПоСчету(Знач счет)
    видыСубконто = счет.ВидыСубконто;

    Если видыСубконто.Количество() = 0 Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат видыСубконто[0].ВидСубконто;
КонецФункции

// Параметры:
//  счет - ПланСчетовСсылка.Хозрасчетный
//  субконто - РегистрБухгалтерииСубконто
//  значение - СправочникСсылка
// Возвращаемое значение:
//  - Булево
Функция заполнитьСубконтоПоСчету(Знач счет, Знач субконто, Знач значение) Экспорт
    видСубконто = получитьВидСубконтоПоСчету(счет);
    Если видСубконто = Неопределено Тогда
        Возврат Ложь;
    КонецЕсли;

    субконто[видСубконто] = значение;
    Возврат Истина;
КонецФункции
#КонецОбласти // ПомощникиПроводок

#КонецОбласти // СлужебныеПроцедурыИФункции
