#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(отказ, __)
    Если проверитьЗаполнениеИНН() = Ложь Тогда
        отказ = Истина;
    КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(_)
    Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
        Возврат;
    КонецЕсли;

    Если ЭтотОбъект.ТипОрганизации <> Перечисления.ТипыОрганизаций.ЮридическоеЛицо
        И ЗначениеЗаполнено(ЭтотОбъект.ГлавныйБухгалтер) Тогда

        ЭтотОбъект.ГлавныйБухгалтер = Справочники.Сотрудники.ПустаяСсылка();
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

Функция проверитьЗаполнениеИНН()
    #Область ДиагностикаПолей
    ДиагностикаКлиентСервер.Утверждение(ЗначениеЗаполнено(ЭтотОбъект.ИНН), "Поле ""ИНН"" должно быть заполнено.");
    ДиагностикаКлиентСервер.Утверждение(ЗначениеЗаполнено(ЭтотОбъект.ТипОрганизации), "Поле ""ТипОрганизации"" должно быть заполнено.");
    ДиагностикаКлиентСервер.Утверждение(
        ЭтотОбъект.ТипОрганизации = Перечисления.ТипыОрганизаций.ЮридическоеЛицо
            ИЛИ ЭтотОбъект.ТипОрганизации = Перечисления.ТипыОрганизаций.ИндивидуальныйПредприниматель,
            "Поле ""ТипОрганизации"" должно иметь значение из списка: [ЮридическоеЛицо, ИндивидуальныйПредприниматель].");
    #КонецОбласти // ДиагностикаПолей

    имяРеквизита = ЭтотОбъект.Метаданные().Реквизиты.ИНН.Имя;
    ожидаемаяДлинаИНН = ?(ЭтотОбъект.ТипОрганизации = Перечисления.ТипыОрганизаций.ЮридическоеЛицо, 10, 12);
    фактическаяДлинаИНН = СтрДлина(ЭтотОбъект.ИНН);
    Если фактическаяДлинаИНН <> ожидаемаяДлинаИНН Тогда
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст = СтрШаблон(
                "Проверьте правильность заполнения поля ""%1"".
                |Длина строки поля ""%1"" должна быть равна ""%2"" знакам, указанная длина строки - ""%3"" знаков.",
                имяРеквизита, ожидаемаяДлинаИНН, фактическаяДлинаИНН);
        сообщение.Поле = имяРеквизита;
        сообщение.УстановитьДанные(ЭтотОбъект);
        сообщение.Сообщить();

        Возврат Ложь;
    КонецЕсли;

    Возврат Истина;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
