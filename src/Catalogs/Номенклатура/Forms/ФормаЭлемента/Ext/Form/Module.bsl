#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(_, __)
    инициализацияНаСервере();
КонецПроцедуры

// Обработчик события формы ПриОткрытии
// Параметры:
// _ - ЭлементФормы - не используется в текущей реализации
&НаКлиенте
Процедура ПриОткрытии(_)
    Если Объект.Ссылка.Пустая() Тогда
        Объект.Родитель = ?(ЗначениеЗаполнено(Объект.Родитель), Объект.Родитель, ЭтотОбъект._Состояние.ГруппыНоменклатуры.Товары);
    КонецЕсли;

    установитьВидимостьПолейФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(отказ, __)
    сообщение = проверитьЗаполнениеРодителя();
    Если проверитьЗаполнениеРодителя() <> Неопределено Тогда
        сообщение.Сообщить();
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(_)
    установитьРодителя();
    установитьВидимостьПолейФормы();
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(_)
    установитьТипНоменклатуры();
    установитьВидимостьПолейФормы();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура установитьРодителя()
    Если Объект.ТипНоменклатуры = ЭтотОбъект._Состояние.ТипыНоменклатуры.Товар Тогда
        Объект.Родитель = ЭтотОбъект._Состояние.ГруппыНоменклатуры.Товары;
    ИначеЕсли Объект.ТипНоменклатуры = ЭтотОбъект._Состояние.ТипыНоменклатуры.Материал Тогда
        Объект.Родитель = ЭтотОбъект._Состояние.ГруппыНоменклатуры.Материалы;
    ИначеЕсли Объект.ТипНоменклатуры = ЭтотОбъект._Состояние.ТипыНоменклатуры.МатериалКлиента Тогда
        Объект.Родитель = ЭтотОбъект._Состояние.ГруппыНоменклатуры.МатериалыКлиента;
    ИначеЕсли Объект.ТипНоменклатуры = ЭтотОбъект._Состояние.ТипыНоменклатуры.Услуга Тогда
        Объект.Родитель = ЭтотОбъект._Состояние.ГруппыНоменклатуры.Услуги;
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура установитьТипНоменклатуры()
    Если Объект.Родитель.Пустая() Тогда
        Возврат;
    КонецЕсли;

    типНоменклатурыГруппы = _Состояние.КартаСоответствияГруппНоменклатур.Получить(Объект.Родитель);
    ДиагностикаКлиентСервер.Утверждение(типНоменклатурыГруппы <> Неопределено,
        "Тип номенклатуры для группы должен быть определен.");

    Объект.ТипНоменклатуры = типНоменклатурыГруппы;
КонецПроцедуры

&НаКлиенте
Процедура установитьВидимостьПолейФормы()
    Элементы.ДлительностьУслуги.Видимость = Объект.ТипНоменклатуры = ЭтотОбъект._Состояние.ТипыНоменклатуры.Услуга;
КонецПроцедуры

// Выполняет проверку заполнения поля Родитель. Если поле не заполнено - формирует сообщение
// с предупреждением для пользователя.
//
// Возвращаемое значение:
//  - СообщениеПользователю - в случае если проверка не пройдена - возвращает сообщение, иначе: Неопределено
//  - Неопределено
//
&НаКлиенте
Функция проверитьЗаполнениеРодителя()
    сообщение = Неопределено;

    Если Объект.Родитель.Пустая() Тогда
        сообщение = Новый СообщениеПользователю();
        сообщение.Текст = "Поле ""Родитель"" не заполнено";
        сообщение.Поле = Элементы.Родитель.Имя;
        сообщение.КлючДанных = Объект.Ссылка;
        сообщение.ПутьКДанным = "Объект";
    КонецЕсли;

    Возврат сообщение;
КонецФункции

&НаКлиенте
Функция получитьКонтекстДиагностики(Знач имяФункции = Неопределено)
    базовыйКонтекстДиагностики = "Номенклатура.ФормаЭлемента";
    Возврат ?(имяФункции = Неопределено, базовыйКонтекстДиагностики, СтрШаблон("%1.%2", базовыйКонтекстДиагностики, имяФункции));
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

#Область Инициализация

&НаСервере
Процедура инициализацияНаСервере()
    ЭтотОбъект._Состояние = Новый Структура("ГруппыНоменклатуры, ТипыНоменклатуры, КартаСоответствияГруппНоменклатур");
    ЭтотОбъект._Состояние.ГруппыНоменклатуры = РаботаСМетаданными.ПолучитьЗначенияПредопределенных(Тип("СправочникСсылка.Номенклатура"));
    ЭтотОбъект._Состояние.ТипыНоменклатуры = Новый ФиксированнаяСтруктура(
            Справочники.Номенклатура.ПолучитьТипыНоменклатуры());
    ЭтотОбъект._Состояние.КартаСоответствияГруппНоменклатур = Справочники.Номенклатура.ПолучитьКартуГруппВТипыНоменклатуры();
КонецПроцедуры

#КонецОбласти // Инициализация
