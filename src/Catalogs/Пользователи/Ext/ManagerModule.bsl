#Область ПрограммныйИнтерфейс

// Параметры:
//	имя - Строка
//	получитьВсеЗаписи - Булево
// Возвращаемое значение:
//	- Структура
//		* Ссылка - СправочникСсылка.Пользователи
//		* Код - Строка
//	- Массив Из Структура
//		* Ссылка - СправочникСсылка.Пользователи
//		* Код - Строка
//	- Неопределено
Функция НайтиПользователяПоИмени(Знач имя, Знач получитьВсеЗаписи = Ложь) Экспорт
    запрос = Новый Запрос;
    запрос.Текст =
        "ВЫБРАТЬ
        |	Пользователи.Ссылка КАК Ссылка,
        |	Пользователи.Код КАК Код
        |ИЗ
        |	Справочник.Пользователи КАК Пользователи
        |ГДЕ
        |	Пользователи.Наименование = &Имя
        |   И Пользователи.ПометкаУдаления = ЛОЖЬ
        |";

    запрос.УстановитьПараметр("Имя", имя);
    результатЗапроса = запрос.Выполнить();

    Если результатЗапроса.Пустой() Тогда
        Возврат Неопределено;
    КонецЕсли;

    тзПользователи = результатЗапроса.Выгрузить();
    пользователь_ключи = "Ссылка, Код";
    Если получитьВсеЗаписи = Ложь Тогда
        пользователь = тзПользователи[0];
        Возврат Новый Структура(пользователь_ключи, пользователь.Ссылка, пользователь.Код);
    КонецЕсли;

    результат = Новый Массив;
    Для Каждого пользовательКЗ Из тзПользователи Цикл
        результат.Вставить(Новый Структура(пользователь_ключи, пользовательКЗ.Ссылка, пользовательКЗ.Код));
    КонецЦикла;

    Возврат результат;
КонецФункции

// Параметры:
//  имя - Строка
//  идентификатор - Строка, Неопределено - Если значение неопределено, будет сгенерирован новый идентификатор
//  дополнительныеПоля - Структура
//  получитьОбъект - Булево - Если Ложь, будет возвращен `Объект` созданного элемента, иначе `Ссылка`
// Возвращаемое значение:
//  - СправочникСсылка.Пользователи - Если `получитьОбъект` = Ложь
//  - СправочникОбъект.Пользователи - Если `получитьОбъект` = Истина
Функция СоздатьПользователя(Знач имя, Знач идентификатор = Неопределено,
        Знач дополнительныеПоля = Неопределено, Знач получитьОбъект = Ложь) Экспорт
    новыйПользователь = Справочники.Пользователи.СоздатьЭлемент();
    новыйПользователь.Наименование = имя;
    идентификатор = ?(идентификатор = Неопределено,
            Строка(Справочники.Пользователи.ПолучитьСсылку().УникальныйИдентификатор()),
            идентификатор);
    новыйПользователь.Код = идентификатор;

    Если дополнительныеПоля <> Неопределено Тогда
        ЗаполнитьЗначенияСвойств(новыйПользователь, дополнительныеПоля);
    КонецЕсли;

    новыйПользователь.Записать();

    Если получитьОбъект = Ложь Тогда
        Возврат новыйПользователь.Ссылка;
    Иначе
        Возврат новыйПользователь;
    КонецЕсли;
КонецФункции

// Выполняет поиск пользователя мобильного приложения
// Параметры:
//  имяИлиИдентификатор - Строка - Имя пользователя или Идентификатор (Код) если `этоИдентификатор` = Истина
//  этоИдентификатор - Булево
// Возвращаемое значение:
//  - Структура
//      * Имя - Строка
//      * УникальныйИдентификатор - Строка
//  - Неопределено - Если пользователь отсутствует
Функция НайтиПользователяПриложения(Знач имяИлиИдентификатор, этоИдентификатор = Ложь) Экспорт
    пользовательСсылка = Неопределено;
    Если этоИдентификатор = Ложь Тогда
        пользовательСсылка = Справочники.Пользователи.НайтиПоНаименованию(имяИлиИдентификатор, Истина);
    Иначе
        пользовательСсылка = Справочники.Пользователи.НайтиПоКоду(имяИлиИдентификатор);
    КонецЕсли;

    Если пользовательСсылка = Неопределено ИЛИ пользовательСсылка.Пустая() Тогда
        Возврат Неопределено;
    КонецЕсли;

    результат = РаботаСРеквизитами.ЗначенияРеквизитовОбъекта(
            пользовательСсылка, Новый Структура("Наименование, Код, Клиент, ПометкаУдаления", "Имя", "УникальныйИдентификатор"));
    Если пользовательСсылка.ПометкаУдаления Тогда
        Возврат Неопределено;
    КонецЕсли;

    результат.Удалить("ПометкаУдаления");
    результат.Вставить("Ссылка", пользовательСсылка);
    Возврат результат;
КонецФункции

// Создает нового пользователя мобильного приложения
// Параметры:
//  имяПользователя - Строка
//  получитьОбъект - Булево - Если Ложь, будет возвращен `Объект` созданного элемента, иначе `Ссылка`
// Возвращаемое значение:
//  - СправочникСсылка.Пользователи - Если `получитьОбъект` = Ложь
//  - СправочникОбъект.Пользователи - Если `получитьОбъект` = Истина
Функция СоздатьПользователяПриложения(Знач имяПользователя, Знач получитьОбъект = Ложь) Экспорт
    Возврат Справочники.Пользователи.СоздатьПользователя(
        имяПользователя, Неопределено, Неопределено, получитьОбъект);
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
